group 'com.srujun'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8

task wrapper(type: Wrapper) {
    description = 'Updates gradlew and supporting files.'
    gradleVersion = '2.3'
}

repositories {
    mavenLocal()
    mavenCentral()
}

// a configuration for dependencies that need exploding into package
configurations {
    explode
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

    compile group: 'org.apache.kafka', name: 'kafka-clients', version: '0.10.1.1'

    compile group: 'org.apache.samza', name: 'samza-api', version: '0.14.0'
    compile group: 'org.apache.samza', name: 'samza-shell', version: '0.14.0'
    compile group: 'org.apache.samza', name: 'samza-core_2.11', version: '0.14.0'
    compile group: 'org.apache.samza', name: 'samza-kafka_2.11', version: '0.14.0'
    compile group: 'org.apache.samza', name: 'samza-kv_2.11', version: '0.14.0'
    compile group: 'org.apache.samza', name: 'samza-yarn_2.11', version: '0.14.0'

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'com.google.guava', name: 'guava', version: '23.5-jre'
}

task distTar(dependsOn: build, type: Tar) {
    destinationDir(new File(project.buildDir, "/distributions"))
    compression(Compression.GZIP)
    classifier('dist')
    extension('tar.gz')

    into('config') {
        from 'src/main/config'
    }

    into("bin") {
        from {
            configurations.explode.collect { tarTree(it) }
        }
    }

    into("lib") {
        from configurations.runtime
        from configurations.runtime.artifacts.files
    }
}

task runSimpleWrite(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'streambench.SimpleWrite'
    args = ["1.2"]
}
